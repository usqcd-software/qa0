;; AST tree printer
#fload "sfc.sf"
#fload "common.sf"
#fload "ast.ss"
;; (provide print-tree)
;;
(define (print-tree ast)
  (define (print-decl decl level)
    (variant-case decl
      [qa0-alias (old-name new-name)
	(print-alias level old-name new-name)]
      [qa0-const (name value)
	(print-const level name value)]
      [qa0-struct (name c-name field-name* field-type* field-c-name*)
	(print-struct level name c-name field-name* field-type*
		      field-c-name*)]
      [qa0-array (name c-name base-name size)
        (print-array level name c-name base-name size)]
      [qa0-proc (attr* name arg-name* arg-type* arg-c-name* arg-c-type* code*)
        (print-proc level attr* name
		    arg-name* arg-type* arg-c-name* arg-c-type* code*)]
      [qa0-verbose (target* data*)
	(print-verbose level target* data*)]
      [qa0-repeat-list (id value* body*)
        (print-repeat-list level id value* body*)]
      [qa0-repeat-range (id low high body*)
        (print-repeat-range level id low high body*)]))
  (define (print-start level)
    (do ([i 0 (+ i 1)])
	[(= i level)]
      (printf "  ")))
  (define (print-verbose level target* data*)
    (print-start level)
    (printf "Verbose (~a) cases:~%" (length target*))
    (do ([target* target* (cdr target*)]
	 [data* data* (cdr data*)])
	[(null? target*)]
      (print-start (+ 1 level))
      (printf "~a: ~s~%" (car target*) (car data*)))
    (print-start level)
    (printf "end(verbose)~%"))
  (define (print-alias level old-name new-name)
    (print-start level)
    (printf "Alias of ~a is ~a~%" old-name new-name))
  (define (print-const level name value)
    (print-start level)
    (printf "Constant ~a = " name)
    (print-input value)
    (printf "~%"))
  (define (print-struct level name c-name field-name* field-type* field-c-name*)
    (print-start level)
    (printf "Structure ~a ~s:~%" name c-name)
    (for-each (lambda (name type c-name)
		(print-start (+ level 1))
		(printf "~a ~a ~s~%" name type c-name))
	      field-name* field-type* field-c-name*))
  (define (print-array level name c-name base-name size)
    (print-start level)
    (printf "Array ~a ~s is ~a of " name c-name base-name)
    (print-input size)
    (printf "~%"))
  (define (print-proc level attr* name
		      arg-name* arg-type* arg-c-name* arg-c-type* code*)
    (print-start level)
    (printf "Procedure ~a attrs " name)
    (print-attr* attr*)
    (printf "~%")
    (for-each (lambda (n t c-n c-t)
		(print-start (+ level 3))
		(printf "[~a ~a ~s ~s]~%" n t c-n c-t))
	      arg-name* arg-type* arg-c-name* arg-c-type*)
    (print-code* (+ level 1) code*)
    (print-start level)
    (printf "Procedure ~a end~%" name))
  (define (print-repeat-list level id value* body*)
    (print-start level)
    (printf "Repeat ~a in ~s~%" id value*)
    (for-each (lambda (decl) (print-decl (+ level 1) decl)) body*)
    (print-start level)
    (printf "Repeat ~a end~%" id))
  (define (print-repeat-range level id low high body*)
    (print-start level)
    (printf "Repeat ~a from " id)
    (print-input low)
    (printf " to ")
    (print-input high)
    (printf "~%")
    (for-each (lambda (decl) (print-decl (+ level 1) decl)) body*)
    (print-start level)
    (printf "Repeat ~a end~%" id))
  (define (print-code* level code*)
    (for-each (lambda (code) (print-code level code)) code*))
  (define (print-code level code)
    (variant-case code
      [qa0-operation (attr* name output* input*)
        (print-operation level attr* name output* input*)]
      [qa0-load (attr* type output addr*)
        (print-load level attr* type output addr*)]
      [qa0-store (attr* type addr* value)
        (print-store level attr* type addr* value)]
      [qa0-loop (attr* var low high code*)
        (print-loop level attr* var low high code*)]
      [qa0-if (var true-code* false-code*)
        (print-if level var true-code* false-code*)]
      [qa0-macro-list (id value* code*)
        (print-macro-list level id value* code*)]
      [qa0-macro-range (id low high code*)
        (print-macro-range level id low high code*)]))
  (define (print-operation level attr* name output* input*)
    (print-start level)
    (printf "op ")
    (print-attr* attr*)
    (printf " (")
    (print-output* output*)
    (printf ") <- ~a (" name)
    (print-input* input*)
    (printf ")~%"))
  (define (print-load level attr* type output addr*)
    (print-start level)
    (printf "load ~a " type)
    (print-attr* attr*)
    (printf " ")
    (print-output output)
    (printf " <- Mem{")
    (print-input* addr*)
    (printf "}~%"))
  (define (print-store level attr* type addr* value)
    (print-start level)
    (printf "store ~a " type)
    (print-attr* attr*)
    (printf " Mem{")
    (print-input* addr*)
    (printf "} <- ")
    (print-input value)
    (printf "~%"))
  (define (print-loop level attr* var low high code*)
    (print-start level)
    (printf "Loop ")
    (print-attr* attr*)
    (printf " ~a from " (reg->name var))
    (print-input low)
    (printf " to ")
    (print-input high)
    (printf "~%")
    (print-code* (+ level 1) code*)
    (print-start level)
    (printf "Loop ~a end~%" (reg->name var)))
  (define (print-if level var true-code* false-code*)
    (print-start level)
    (printf "If ")
    (print-input var)
    (printf " then~%")
    (print-code* (+ level 1) true-code*)
    (print-start level)
    (printf "else~%")
    (print-code* (+ level 1) false-code*)
    (print-start level)
    (printf "fi~%"))
  (define (print-macro-list level id value* body*)
    (print-start level)
    (printf "Macro ~a in ~s~%" id value*)
    (print-code* (+ level 1) body*)
    (print-start level)
    (printf "Macro ~a end~%" id))
  (define (print-macro-range level id low high body*)
    (print-start level)
    (printf "Macro ~a from " id)
    (print-input low)
    (printf " to ")
    (print-input high)
    (printf "~%")
    (print-code* (+ level 1) body*)
    (print-start level)
    (printf "Macro ~a end~%" id))
  (define (print-input* input*) (print-list print-input input*))
  (define (print-output* output*) (print-list print-output output*))
  (define (print-attr* attr*)
    (printf "(") (print-list print-attr attr*) (printf ")"))
  (define (print-list print-it l*)
    (if (null? l*) #f
	(begin (print-it (car l*))
	       (do ([l* (cdr l*) (cdr l*)])
		   [(null? l*)]
		 (printf " ")
		 (print-it (car l*))))))
  (define (write-list l*)
    (if (null? l*) #f
	(begin (write (car l*))
	       (do ([l* (cdr l*) (cdr l*)])
		   [(null? l*)]
		 (printf " ")
		 (write (car l*))))))
  (define (print-attr attr)
    (variant-case attr
      [qa0-attr (name value*)
	(if (null? value*) (printf "~a" name)
	    (begin (printf "[~a " name)
		   (write-list value*)
		   (printf "]")))]))
  (define (print-output out)
    (variant-case out
      [reg (name) (printf "~a" name)]))
  (define (print-input in)
    (variant-case in
      [reg (name) (printf "[reg ~a]" name)]
      [else (printf "[const ") (print-cexpr in) (printf "]")]))
  (define (print-cexpr ce)
    (variant-case ce
      [c-expr-quote (literal) (printf "'~a" literal)]
      [c-expr-id (id) (printf "~a" id)]
      [c-expr-number (number) (printf "~a" number)]
      [c-expr-string (string) (printf "~s" string)]
      [c-expr-op (name c-expr*) (printf "(~a " name)
		                (print-list print-cexpr c-expr*)
				(printf ")")]))
  (variant-case ast
    [qa0-top (decl*) (for-each (lambda (decl) (print-decl decl 0)) decl*)]))

